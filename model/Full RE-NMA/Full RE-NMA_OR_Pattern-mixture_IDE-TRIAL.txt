###############################################################################################
#                                                                                             #
#       Random-effects NMA model with consistency and accommodation of multi-arm trials       #
#                      <Binomial likelihood, logit link, Random Effects>                      #
#                      (Dias et al., 2013 in Appendix - PMID: 23104435)                       #
#                               One-stage pattern-mixture model                               #
#                           (Spineli et al., 2019 - PMID: 31018836)                           #
#      Odds ratio (OR) and Informative Missingness Odds ratio (IMOR) in logarithmic scale     #
#                            <Identical, trial-specific log IMOR>                             #
#                           (Turner et al., 2015 - PMID: 25809313)                            #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){
      w[i, 1] <- 0

      ## Baseline log odds of event
      logit(p[i, 1]) <- u[i] 
      u[i] ~ dnorm(0, .0001)

      for(k in 1:na[i]){  
        ## Observed events and likelihood per arm
        r[i, k] ~ dbin(p_o[i, k], obs[i, k])
        obs[i, k] <- N[i, k] - m[i, k]
        p_o[i, k] <- max(0, min(1, ((-((q[i, k] - p[i, k])*(1 - theta[i, k]) - 1) - sqrt((pow(((q[i, k] - p[i, k])*(1 - theta[i, k]) - 1), 2)) - 
                                   ((4*p[i, k])*(1 - q[i, k])*(1 - theta[i, k]))))/(2*(1 - q[i, k])*(1 - theta[i, k]))))) 

        ## Missing participants and likelihood per arm
        m[i, k] ~ dbin(q[i, k], N[i, k])
        q[i, k] ~ dunif(0, 1)

        ## Identical, trial-specific logIMOR 
        log(theta[i, k]) <- phi[i]
                        }

    for(k in 2:na[i]){
      logit(p[i, k]) <- u[i] + delta[i, k]
      delta[i, k] ~ dnorm(md[i, k], precd[i, k])
      md[i, k] <- d[t[i, k]] - d[t[i, 1]] + sw[i, k]
      w[i, k] <- delta[i, k] - (d[t[i, k]] - d[t[i, 1]])
      sw[i, k] <- sum(w[i, 1:(k - 1)])/(k - 1)
      precd[i, k] <- 2*(k - 1)*prec/k
                     }
                   }  

    ## Prior consideration on identical, trial-specific logIMOR
    for(i in 1:ns){
      phi[i] ~ dnorm(meand.phi, precd.phi)
                  } 

    ## Basic parameters - prior distributions
    d[ref] <- 0
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, .0001)
                         }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, .0001)
                          }

    ## Ranking probabilities and SUCRA values
    sorted <- rank(d[])
    for(t in 1:nt){
      order[t] <- (nt + 1 - sorted[t])*equals(D, 1) + sorted[t]*(1 - equals(D, 1))
      most.effective[t] <- equals(order[t], 1)

      for(l in 1:nt){
        effectiveness[t, l] <- equals(order[t], l)
        cumeffectiveness[t, l] <- sum(effectiveness[t, 1:l])
                      }

      SUCRA[t] <- sum(cumeffectiveness[t, 1:(nt - 1)])/(nt - 1)
                     }  

    ## Obtain reference-specific logORs (effect measure; EME)
    for(t in 1:(ref - 1)){
      EM.ref[t] <- d[t] - d[ref]
                          }

    for(t in (ref + 1):nt){
      EM.ref[t] <- d[t] - d[ref]
                          }

    ## Collect all possible (unique) pairwise comparisons
    for(c in 1:(nt - 1)){
      for(k in (c + 1):nt){
        EM[k, c] <- d[k] - d[c]
                          }
                        }  

    ## Weakly-informative prior on between-trial standard deviation
    prec <- pow(sd, -2)                                                                  
    sd ~ dnorm(0, 1)I(0, ) 
    tau2 <- sd*sd 
    } # END OF MODEL





