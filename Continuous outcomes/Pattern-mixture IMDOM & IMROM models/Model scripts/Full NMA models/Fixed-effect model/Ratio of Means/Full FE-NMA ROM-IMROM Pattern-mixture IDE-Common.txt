###############################################################################################
#                                                                                             #
#                      Fixed-effect NMA model with consistency equation                       #
#                      <Normal likelihood, identity link, Fixed Effect>                       #
#                      (Dias et al., 2013 in Appendix  - PMID: 23104435)                      #
#                               One-stage pattern-mixture model                               #
#           Ratio of Means (ROM) and Informative Missingness Ratio of Means (IMROM)           #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#       <Identical, common-within-network log IMROM under missing at random assumption>       #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){                                                                                      # loop through trials

      ## Baseline mean value
      theta[i, 1] <- u[i]
      u[i] ~ dnorm(0, 0.0001)

      for(k in 1:na[i]){                                                                                 # loop through all arms in trial i
        ## Observed mean value and precision per arm & linking equation
        prec.o[i, k] <- pow(se.o[i, k], -2)                                                              # observed precision of the outcome
        y.o[i, k] ~ dnorm(theta.o[i, k], prec.o[i, k])                                                   # normal likelihood for the oberved mean of the outcome
        theta.o[i, k] <- theta[i, k]/(1 - q[i, k]*(1 - exp(phi)))                                        # linking equation for Informative Missingness Ratio of Means (IMRoM)

        ## Missing participants and likelihood per arm
        m[i, k] ~ dbin(q[i, k], N[i, k])                                                                 # binomial likelihood for missing outcome data (MOD)
        q[i, k] ~ dunif(0, 1)                                                                            # uniform likelihood for the probability of MOD

        ## Deviance contribution for observed and missing data
        dev.o[i, k] <- (y.o[i, k] - theta.o[i, k])*(y.o[i, k] -                                          # Deviance contribution (observed data; PMID: 23104435)
                        theta.o[i, k])*prec.o[i, k]
        m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                                                    # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
        rhat.m[i, k] <- q[i, k]*N[i, k]                                                                  # Expected value of numerators (missing data; PMID: 25809313)
        dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(rhat.m[i, k])) +                                 # Deviance contribution (MOD; PMID: 25809313)
                       (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k]) - 
                       log(N[i, k] - rhat.m[i, k])))
                       }

      resdev.o[i] <- sum(dev.o[i, 1:na[i]])                                                              # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
      resdev.m[i] <- sum(dev.m[i, 1:na[i]])                                                              # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)
      
      for(k in 2:na[i]){                                       
        theta[i, k] <- u[i]*exp(d[t[i, k]] - d[t[i, 1]])                                                 # link function for ratio of means (RoM)
                      }
                   }

    totresdev.o <- sum(resdev.o[])                                                                       # Total Residual Deviance (observed data; PMID: 23104435)
    totresdev.m <- sum(resdev.m[])                                                                       # Total Residual Deviance (MOD; PMID: 25809313)

    ## Basic parameters - prior distribution
    d[ref] <- 0                                                                                          # reference intervention in the network
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, 0.0001)                                                                            # normal distribution for basic parameters
                         }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, 0.0001)                                                                            # normal distribution for basic parameters
                          }

    ## Prior consideration on identical, common-within-network log IMROM
    phi ~ dnorm(0, prec.phi)                                                                             # on average missing at random with uncertainty (specified in R script)

    ## Ranking probabilities and SUCRA values
    sorted <- rank(d[])
    for(t in 1:nt){
      order[t] <- (nt + 1 - sorted[t])*equals(D, 1) + sorted[t]*(1 - equals(D, 1))
      most.effective[t] <- equals(order[t], 1)

      for(l in 1:nt){
        effectiveness[t, l] <- equals(order[t], l)
        cumeffectiveness[t, l] <- sum(effectiveness[t, 1:l])
                    }

      SUCRA[t] <- sum(cumeffectiveness[t, 1:(nt - 1)])/(nt - 1)
                  }  

    ## Obtain reference-specific log RoMs
    for(t in 1:(ref - 1)){
      LRoM.ref[t] <- d[t] - d[ref]
                        }

    for(t in (ref + 1):nt){
      LRoM.ref[t] <- d[t] - d[ref]
                          }

    ## Obtain all possible (unique) pairwise comparisons
    for(c in 1:(nt - 1)){
      for(k in (c + 1):nt){
        LRoM[k, c] <- d[k] - d[c]
                          }
                         }
    } # END OF MODEL   

                              
