###############################################################################################
#                                                                                             #
#                       Fixed-effect NMA model with consistency equation                      #
#                       <Normal likelihood, identity link, Fixed Effect>                      #
#                      (Dias et al., 2013 in Appendix  - PMID: 23104435)                      #
#                               One-stage pattern-mixture model                               #
#        Mean Difference (MD) and Informative Missingness Difference of Means (IMDOM)         #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#       <Independent, within-trial correlated IMDOM under missing at random assumption>       #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){                                                           # loop through trials

      ## Baseline mean value
      theta[i, 1] <- u[i] 
      u[i] ~ dnorm(0, .0001)

      for(k in 1:na[i]){                                                      # loop through all arms in trial i
        ## Observed events and likelihood per arm
        prec.o[i, k] <- pow(se.o[i, k], -2)                                   # observed precision of the outcome
        y.o[i, k] ~ dnorm(theta.o[i, k], prec.o[i, k])                        # normal likelihood for the oberved mean of the outcome
        theta.o[i, k] <- theta[i, k] - phi[i, k]*q[i, k]                      # linking equation for Informative Missingness Difference of Means (IMDoM)

        ## Missing participants and likelihood per arm
        m[i, k] ~ dbin(q[i, k], N[i, k])                                      # binomial likelihood for missing outcome data (MOD)
        q[i, k] ~ dunif(0, 1)                                                 # uniform likelihood for the probability of MOD

        ## Deviance contribution for observed and missing data
        dev.o[i, k] <- (y.o[i, k] - theta.o[i, k])*(y.o[i, k] -               # Deviance contribution (observed data; PMID: 23104435)
                        theta.o[i, k])*prec.o[i, k]
        m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                         # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
        rhat.m[i, k] <- q[i, k]*N[i, k]                                       # Expected value of numerators (missing data; PMID: 25809313)
        dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(rhat.m[i, k])) +      # Deviance contribution (MOD; PMID: 25809313)
                       (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k]) - 
                       log(N[i, k] - rhat.m[i, k])))
       
        for(l in 1:na[i]){
          V[i, k, l] <- cov.phi*(1 - equals(k, l)) + var.phi*equals(k, l)     # variance-covariance matrix for multivariate IMDoM (variance specified in R script and correlation equal to 0.5)
                         }
                        }

      Omega[i, 1:na[i], 1:na[i]] <- inverse(V[i, 1:na[i], 1:na[i]])           # Precision matrix for multivariate IMDoM
      phi[i, 1:na[i]] ~ dmnorm(M[i, 1:na[i]], Omega[i, 1:na[i], 1:na[i]])     # independent, multivariate IMDoM

      resdev.o[i] <- sum(dev.o[i, 1:na[i]])                                   # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
      resdev.m[i] <- sum(dev.m[i, 1:na[i]])                                   # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)  
    
      for(k in 2:na[i]){                                       
        theta[i, k] <- u[i] + (d[t[i, k]] - d[t[i, 1]])                       # link function for mean difference (MD)
                       }
                    }

    totresdev.o <- sum(resdev.o[])                                            # Total Residual Deviance (observed data; PMID: 23104435)
    totresdev.m <- sum(resdev.m[])                                            # Total Residual Deviance (MOD; PMID: 25809313)

    ## Basic parameters - prior distributions
    d[ref] <- 0                                                               # reference intervention in the network
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, 0.0001)                                                 # normal distribution for basic parameters
                         }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, 0.0001)                                                 # normal distribution for basic parameters
                          }

    ## Ranking probabilities and SUCRA values
    sorted <- rank(d[])
    for(t in 1:nt){
      order[t] <- (nt + 1 - sorted[t])*equals(D, 1) + sorted[t]*(1 - equals(D, 1))
      most.effective[t] <- equals(order[t], 1)

      for(l in 1:nt){
        effectiveness[t, l] <- equals(order[t], l)
        cumeffectiveness[t, l] <- sum(effectiveness[t, 1:l])
                    }

      SUCRA[t] <- sum(cumeffectiveness[t, 1:(nt - 1)])/(nt - 1)
                  }  

    ## Obtain reference-specific MDs
    for(t in 1:(ref - 1)){
      MD.ref[t] <- d[t] - d[ref]
                         }

    for(t in (ref + 1):nt){
      MD.ref[t] <- d[t] - d[ref]
                          }

    ## Obtain all possible (unique) pairwise comparisons
    for(c in 1:(nt - 1)){
      for(k in (c + 1):nt){
        MD[k, c] <- d[k] - d[c]
                           }
                         }
    } # END OF MODEL

                              