###############################################################################################
#                                                                                             #
#       Random-effects NMA model with consistency and accommodation of multi-arm trials       #
#                     <Normal likelihood, identity link, Random Effects>                      #
#                      (Dias et al., 2013 in Appendix  - PMID: 23104435)                      #
#                               One-stage pattern-mixture model                               #
#        Mean Difference (MD) and Informative Missingness Difference of Means (IMDOM)         #
#                           (Mavridis et al., 2015 - PMID: 25393541)                          #
#       <Hierarchical, common-within-network IMDOM under missing at random assumption>        #
#                                                                                             #
###############################################################################################


model{
    for(i in 1:ns){                                                       # loop through trials
      w[i, 1] <- 0

      ## Baseline mean value
      theta[i, 1] <- u[i]
      u[i] ~ dnorm(0, 0.0001)

      for(k in 1:na[i]){                                                  # loop through all arms in trial i
        ## Observed mean value and precision per arm & linking equation
        prec.o[i, k] <- pow(se.o[i, k], -2)                               # observed precision of the outcome
        y.o[i, k] ~ dnorm(theta.o[i, k], prec.o[i, k])                    # normal likelihood for the oberved mean of the outcome
        theta.o[i, k] <- theta[i, k] - phi[i, k]*q[i, k]                  # linking equation for Informative Missingness Difference of Means (IMDoM)
        phi[i, k] ~ dnorm(mean.phi, prec.phi)                             # hierarchical, common-within-network IMDoM

        ## Missing participants and likelihood per arm
        m[i, k] ~ dbin(q[i, k], N[i, k])                                  # binomial likelihood for missing outcome data (MOD)
        q[i, k] ~ dunif(0, 1)                                             # uniform likelihood for the probability of MOD

        ## Deviance contribution for observed and missing data
        dev.o[i, k] <- (y.o[i, k] - theta.o[i, k])*(y.o[i, k] -           # Deviance contribution (observed data; PMID: 23104435)
                        theta.o[i, k])*prec.o[i, k]
        m0[i, k] <- m[i, k] + 0.01*equals(m[i, k], 0)                     # Correction for zero MOD in arm k od trial i (PMID: 25809313) 
        rhat.m[i, k] <- q[i, k]*N[i, k]                                   # Expected value of numerators (missing data; PMID: 25809313)
        dev.m[i, k] <- 2*(m0[i, k]*(log(m0[i, k]) - log(rhat.m[i, k])) +  # Deviance contribution (MOD; PMID: 25809313)
                       (N[i, k] - m0[i, k])*(log(N[i, k] - m0[i, k]) - 
                       log(N[i, k] - rhat.m[i, k])))
                       }

      resdev.o[i] <- sum(dev.o[i, 1:na[i]])                               # Summed residual deviance contribution for this trial (observed data; PMID: 23104435)
      resdev.m[i] <- sum(dev.m[i, 1:na[i]])                               # Summed residual deviance contribution for this trial (MOD; PMID: 25809313)
      
      for(k in 2:na[i]){                                       
        theta[i, k] <- u[i] + delta[i, k]                                 # link function for mean difference (MD)
        delta[i, k] ~ dnorm(md[i, k], precd[i, k])                        # within-trial true MD (random-effect)
        md[i, k] <- d[t[i, k]] - d[t[i, 1]] + sw[i, k]                    # mean of MD distributions (with multi-arm trial correction)
        precd[i, k] <- 2*prec*(k - 1)/k                                   # precision of MD distributions (with multi-arm trial correction)
        w[i, k] <- delta[i, k] - (d[t[i, k]] - d[t[i, 1]])                # adjustment for multi-arm trials
        sw[i, k] <- sum(w[i, 1:(k - 1)])/(k - 1)                          # cumulative adjustment for multi-arm trials
                        }
                      }

    totresdev.o <- sum(resdev.o[])                                        # Total Residual Deviance (observed data; PMID: 23104435)
    totresdev.m <- sum(resdev.m[])                                        # Total Residual Deviance (MOD; PMID: 25809313)

    ## Basic parameters - prior distributions
    d[ref] <- 0                                                           # reference intervention in the network
    for(t in 1:(ref - 1)){
      d[t] ~ dnorm(0, 0.0001)                                             # normal distribution for basic parameters
                         }

    for(t in (ref + 1):nt){
      d[t] ~ dnorm(0, 0.0001)                                             # normal distribution for basic parameters
                          }

    ## Prior consideration on hyperparameters of hierarchical, common-within-network IMDOM
    mean.phi ~ dnorm(0, precd.phi)                                        # on average missing at random with uncertainty (specified in R script)
    prec.phi <- pow(sd.phi, -2)
    sd.phi ~ dunif(0, psi.phi) 

    ## Ranking probabilities and SUCRA values
    sorted <- rank(d[])
    for(t in 1:nt){
      order[t] <- (nt + 1 - sorted[t])*equals(D, 1) + sorted[t]*(1 - equals(D, 1))
      most.effective[t] <- equals(order[t], 1)

      for(l in 1:nt){
        effectiveness[t, l] <- equals(order[t], l)
        cumeffectiveness[t, l] <- sum(effectiveness[t, 1:l])
                    }

      SUCRA[t] <- sum(cumeffectiveness[t, 1:(nt - 1)])/(nt - 1)
                   }  

    ## Obtain reference-specific MDs
    for(t in 1:(ref - 1)){
      MD.ref[t] <- d[t] - d[ref]
                         }

    for(t in (ref + 1):nt){
      MD.ref[t] <- d[t] - d[ref]
                          }

    ## Obtain all possible (unique) pairwise comparisons
    for(c in 1:(nt - 1)){
      for(k in (c + 1):nt){
        MD[k, c] <- d[k] - d[c]
                          }
                         }

    ## Weakly-informative prior on between-trial standard deviation
    prec <- pow(sd, -2)                                                                  
    sd ~ dnorm(0, 1)I(0, ) 
    tau2 <- sd*sd  
    } # END OF MODEL 

                              